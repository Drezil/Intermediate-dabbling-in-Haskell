\documentclass{beamer}

\usepackage[utf8]{inputenc}
\usepackage[T1]{fontenc}
\usepackage[ngerman]{babel}
\usepackage{graphicx} % Bilder
\usepackage{wrapfig} % Umflussbilder
\usepackage{multicol} % Multiple columns
\usepackage{minted} % Haskell source code
\usepackage{framed} % Frames around source code
\usepackage[framemethod=tikz]{mdframed} % Frames
\usepackage{verbatim} % \begin{comment}...\end{comment}
\usepackage{etoolbox} % manipulate minted
\AtBeginEnvironment{minted}{\fontsize{8}{8}\selectfont}
\AfterEndEnvironment{minted}{}

\mdfdefinestyle{fancy}{
  roundcorner=5pt,
  linewidth=4pt,
  linecolor=red!80,
  backgroundcolor=red!20
}
\newmdenv[style=fancy]{important}

% Stuff for Beamer
\beamertemplatenavigationsymbolsempty
\usetheme{Warsaw}

\title{Intermediate Dabbling in Haskell}

\begin{document}

%  \usebackgroundtemplate{\includegraphics[width=\paperwidth,height=\paperheight]{1.jpg}} 
  
%----------------------------------------------------------------------------------------  

\begin{frame}
  \begin{center}
    \Huge\textbf{Intermediate Functional Programming in Haskell}\\ \bigskip
    \LARGE Universität Bielefeld, Sommersemester 2015\\ \bigskip
    \large Jonas Betzendahl \& Stefan Dresselhaus
    \end{center}
\end{frame}

%----------------------------------------------------------------------------------------  
\begin{frame}[allowframebreaks]{Outline}
\frametitle{Übersicht}
\tableofcontents[hideallsubsections]
\end{frame}

\section{Wozu brauchen wir das überhaupt?}

\begin{frame}[fragile]
\textbf{Wozu brauchen wir lens überhaupt?}\\
\bigskip
Die Idee dahinter ist, dass man Zugriffsabstraktionen über Daten
verknüpfen kann. Als einfachen Datenstruktur kann man einen Record mit
der entsprechenden Syntax nehmen.
\end{frame}

\subsection{Beispiel}

\begin{frame}[fragile]
Nehmen wir folgende Datenstruktur an:
\tiny
\begin{minted}{haskell}
data Person = P { name :: String
                , addr :: Address
                , salary :: Int }
data Address = A { road :: String
                 , city :: String
                 , postcode :: String }
-- autogeneriert unten anderem: addr :: Person -> Address
    
setName :: String -> Person -> Person
setName n p = p { name = n } --record update notation
    
setPostcode :: String -> Person -> Person
setPostcode pc p = p { addr = addr p { postcode = pc } }
-- update of a record inside a record
\end{minted}
\normalsize
\end{frame}

\subsection{Probleme}
\begin{frame}[fragile]
Probleme mit diesem Code:

\begin{itemize}
\item
  für 1-Dimensionale Felder ist die record-syntax ok.
\item
  tiefere Ebenen nur umständlich zu erreichen
\item
  eigentlich wollen wir nur pc in p setzen, müssen aber über addr etc.
  gehen.
\item
  wir brauchen wissen über die ``Zwischenstrukturen'', an denen wir
  nicht interessiert sind
\end{itemize}
\end{frame}

\subsection{Was wir gern hätten}
\begin{frame}[fragile]
Was wir gerne hätten:

\begin{minted}{haskell}
data Person = P { name :: String
                , addr :: Address
                , salary :: Int }
-- a lens for each field
lname   :: Lens' Person String
laddr   :: Lens' Person Adress
lsalary :: Lens' Person Int
-- getter/setter for them
view    :: Lens' s a -> s -> a
set     :: Lens' s a -> a -> s -> s
-- lens-composition
composeL :: Lens' s1 s2 -> Lens s2 a -> Lens' s1 a
\end{minted}
\end{frame}

\subsection{Wie uns das hilft}
\begin{frame}[fragile]
Mit diesen Dingen (wenn wir sie hätten) könnte man dann

\begin{minted}{haskell}
data Person = P { name :: String
                , addr :: Address
                , salary :: Int }
data Address = A { road :: String
                 , city :: String
                 , postcode :: String }
setPostcode :: String -> Person -> Person
setPostcode pc p
    = set (laddr `composeL` lpostcode) pc p
\end{minted}

machen und wäre fertig.
\end{frame}

\section{Trivialer Ansatz}

\subsection{Getter/Setter als Lens-Methoden}
\begin{frame}[fragile]
\begin{minted}{haskell}
data LensR s a = L { viewR :: s -> a
                   , setR  :: a -> s -> s }

composeL (L v1 u1) (L v2 u2)
  = L (\s -> v2 (v1 s))
      (\a s -> u1 (u2 a (v1 s)) s)
\end{minted}
\end{frame}

\begin{frame}[fragile]
Wieso ist das schlecht?\\
\pause
\bigskip
Dies ist extrem ineffizient:\\
Auslesen traversiert die Datenstruktur, dann wird die Funktion
angewendet und zum setzen wird die Datenstruktur erneut traversiert:

\begin{minted}{haskell}
over :: LensR s a -> (a -> a) -> s -> s
over ln f s = setR l (f (viewR l s)) s
\end{minted}
\pause
Lösung: modify-funktion hinzufügen

\end{frame}

\begin{frame}[fragile]
\begin{minted}{haskell}
data LensR s a
   = L { viewR :: s -> a
       , setR  :: a -> s -> s
       , mod   :: (a->a) -> s -> s
       , modM  :: (a->Maybe a) -> s -> Maybe s
       , modIO :: (a->IO a) -> s -> IO s }
\end{minted}
\pause
Neues Problem: Für jeden Spezialfall muss die Lens erweitert werden.
\end{frame}

\subsection{Something in common}
\begin{frame}[fragile]
Man kann alle Monaden abstrahieren. Functor reicht schon:

\begin{minted}{haskell}
data LensR s a
   = L { viewR :: s -> a
       , setR  :: a -> s -> s
       , mod   :: (a->a) -> s -> s
       , modF  :: Functor f => (a->f a) -> s -> f s }
\end{minted}
\pause

Idee: Die 3 darüberliegenden durch modF ausdrücken.
\end{frame}

\subsection{Typ einer Lens}
\begin{frame}[fragile]
Wenn man das berücksichtigt, dann hat einen Lens folgenden Typ:

\begin{minted}{haskell}
type Lens' s a = forall f. Functor f
                           => (a -> f a) -> s -> f s
\end{minted}
\pause
Allerdings haben wir dann noch unseren getter/setter:

\begin{minted}{haskell}
data LensR s a = L { viewR :: s -> a
                   , setR :: a -> s -> s }
\end{minted}
\pause
Stellt sich raus: Die sind isomorph! Auch wenn die von den Typen her
komplett anders aussehen.
\end{frame}

\section{Lenses als Getter/Setter}
\subsection{Benutzen einer Lens als Setter}
\begin{frame}[fragile]
\begin{minted}{haskell}
set :: Lens' s a -> (a -> s -> s)
set ln a s = --...umm...
--:t ln => (a -> f a) -> s -> f s
--            => get s out of f s to return it
\end{minted}

Wir können für f einfach die ``Identity''-Monade nehmen, die wir nachher
wegcasten können.
\end{frame}

\begin{frame}[fragile]
Zur Erinnerung kurz nochmal die Definition:
\begin{minted}{haskell}
newtype Identity a = Identity a
-- Id :: a -> Identity a

runIdentity :: Identity s -> s
runIdentity   (Identity x) = x

instance Functor Identity where
    fmap f (Identity x) = Identity (f x)
\end{minted}
\end{frame}

\begin{frame}[fragile]
somit ist set einfach nur

\begin{minted}{haskell}
set :: Lens' s a -> (a -> s -> s)
set ln x s
   = runIdentity (ln set_fld s)
   where
     set_fld :: a -> Identity a
     set_fld _ = Identity x
     -- a was the OLD value.
     -- We throw that away and set the new value
\end{minted}
\pause
oder kürzer (für nerds wie den Autor der Lens-Lib)

\begin{minted}{haskell}
set :: Lens' s a -> (a -> s -> s)
set ln x = runIdentity . ln (Identity . const x)
\end{minted}
\end{frame}


\subsection{Benutzen einer Lens als Modify}
\begin{frame}[fragile]
\textbf{Wie nutzen wir das nun als modify?}\\
Dasselbe wie set, nur dass wir den Parameter nicht entsorgen, sondern in
die mitgelieferte Funktion stopfen.
\begin{minted}{haskell}
over :: Lens' s a -> (a -> a) -> s -> s
over ln f = runIdentity . ln (Identity . f)
\end{minted}
\end{frame}

\subsection{Benutzen einer Lens als Getter}
\begin{frame}[fragile]
\textbf{.. und als getter?}
\begin{minted}{haskell}
view :: Lens' s a -> (s -> a)
view ln s = --...umm...
--:t ln => (a -> f a) -> s -> f s
--            => get a out of the (f s) return-value
--            Wait, WHAT?
\end{minted}
\pause
Auch hier gibt es einen netten Funktor. Wir packen das \texttt{a} einfach in
das \texttt{f} und werfen das \texttt{s} am Ende weg.
\pause
\begin{minted}{haskell}
newtype Const v a = Const v

getConst :: Const v a -> v
getConst (Const x) = x

instance Functor (Const v) where
    fmap f (Const x) = Const x
    -- throw f away. Nothing changes our const!
\end{minted}
\end{frame}

\begin{frame}[fragile]
somit ergibt sich

\begin{minted}{haskell}
view :: Lens' s a -> (s -> a)
view ln s 
  = getConst (ln Const s)
              -- Const :: s -> Const a s
\end{minted}
\pause
oder nerdig

\begin{minted}{haskell}
view :: Lens' s a -> (s -> a)
view ln = getConst . ln Const
\end{minted}
\end{frame}

\subsection{Lenses bauen}
\begin{frame}[fragile]
Nochmal kurz der Typ:

\begin{minted}{haskell}
type Lens' s a = forall f. Functor f
                     => (a -> f a) -> s -> f s
\end{minted}
\pause
Für unser Personen-Beispiel vom Anfang:
\small
\begin{minted}{haskell}
data Person = P { _name :: String, _salary :: Int }

name :: Lens' Person String
-- name :: Functor f => (String -> f String)
--                    -> Person -> f Person
name elt_fn (P n s)
  = fmap (\n' -> P n' s) (elt_fn n)
-- fmap :: Functor f => (a->b) -> f a -> f b 
-- - der Funktor, der alles verknüpft
-- \n' -> .. :: String -> Person 
-- - Funktion um das Element zu lokalisieren (WO wird ersetzt/gelesen/...)
-- elt_fn n  :: f String         
-- - Funktion um das Element zu veraendern (setzen, aendern, ...)
\end{minted}
\normalsize
Die Lambda-Funktion ersetzt einfach den Namen.\\
\pause
Häufig sieht man auch

\begin{minted}{haskell}
name elt_fn (P n s)
  = (\n' -> P n' s) <$> (elt_fn n)
--  |    Focus    |     |Function|
\end{minted}
\end{frame}

\section{Funktionsweise}
\subsection{Wie funktioniert das intern?}
\begin{frame}[fragile]
Ist das nicht alles ein ziemlicher Overhead?
\pause

\begin{minted}{haskell}
view name (P {_name="Fred", _salary=100})
   -- inline view-function
= getConst (name Const (P {_name="Fred", _salary=100})
   -- inline name
= getConst (fmap (\n' -> P n' 100) (Const "Fred"))
   -- fmap f (Const x) = Const x - Definition von Const
= getConst (Const "Fred")
   -- getConst (Const x) = x
= "Fred"
\end{minted}
\pause
Dieser Aufruf hat KEINE Runtime-Kosten, weil der Compiler direkt die
Adresse des Feldes einsetzen kann. Der gesamte Boilerplate-Code wird vom
Compiler wegoptimiert.

Dies gilt für jeden Funktor mit newtype, da das nur ein Typalias ist.
\end{frame}

\subsection{Composing Lenses und deren Benutzung}

\begin{frame}[fragile]
Wie verknüpfen wir lenses nun?\\
\pause
Schauen wir uns die Typen an:\\
Wir wollen ein
\begin{minted}{haskell}
Lens' s1 s2 -> Lens' s2 a -> Lens' s1 a
\end{minted}
\pause
Wir haben 2 Lenses
\begin{minted}{haskell}
ln1 :: (s2 -> f s2) -> (s1 -> f s1)
ln2 :: (a -> f a) -> (s2 -> f s2)
\end{minted}
\pause
wenn man scharf hinsieht, kann man die verbinden
\begin{minted}{haskell}
ln1 . ln2 :: (a -> f s) -> (s1 -> f s1)
\end{minted}
\pause
und erhält eine Lens. Sogar die Gewünschte!\\Somit ist Lens-Composition
einfach nur Function-Composition (.).
\end{frame}

\subsection{Automatisieren mit Template-Haskell}

\begin{frame}[fragile]
Der Code um die Lenses zu bauen ist für records immer Identisch:

\begin{minted}{haskell}
data Person = P { _name :: String, _salary :: Int }

name :: Lens' Person String
name elt_fn (P n s) = (\n' -> P n' s) <$> (elt_fn n)
\end{minted}
\pause
Daher kann man einfach

\begin{minted}{haskell}
import Control.Lens.TH
data Person = P { _name :: String, _salary :: Int }

$(makeLenses ''Person)
\end{minted}

nehmen, was einem eine Lens für ``name'' und eine Lens für ``salary''
generiert.\\Mit anderen Templates kann man auch weitere Dinge steuern
(etwa wofür Lenses generiert werden, welches Prefix (statt \_) man haben
will etc. pp.).\\Will man das aber haben, muss man selbst in den
Control.Lens.TH-Code schauen.
\end{frame}

\subsection{Lenses für den Beispielcode}
\begin{frame}[fragile]
Noch ein Beispiel:
\begin{minted}{haskell}
import Control.Lens.TH

data Person = P { _name :: String
                , _addr :: Address
                , _salary :: Int }
data Address = A { _road :: String
                 , _city :: String
                 , _postcode :: String }

$(makeLenses ''Person)
$(makeLenses ''Address)

setPostcode :: String -> Person -> Person
setPostcode pc p = set (addr . postcode) pc p
\end{minted}
\end{frame}

\subsection{Shortcuts mit ``Line-Noise''}
\begin{frame}[fragile]
Für alle gängigen Funktionen gibt es auch infix-Operatoren:
\begin{minted}{haskell}
setPostcode :: String -> Person -> Person
setPostcode pc p = addr . postcode .~ pc     $ p
--                 |   Focus     |set|to what|in where

getPostcode :: Person -> String
getPostcode p = p   ^. $ addr . postcode
--            |from|get|    Focus       |
\end{minted}

Es gibt momentan viele weitere Infix-Operatoren (für Folds,
Listenkonvertierungen, -traversierungen, \ldots{}). Aktueller Stand: 80+
\end{frame}

\subsection{Virtuelle Felder}
\begin{frame}[fragile]
Man kann mit Lenses sogar Felder emulieren, die gar nicht da sind.\\
\pause
Angenommen folgender Code:

\begin{minted}{haskell}
data Temp = T { _fahrenheit :: Float }

$(makeLenses ''Temp)
-- liefert Lens: fahrenheit :: Lens Temp Float

centigrade :: Lens Temp Float
centigrade centi_fn (T faren)
  = (\centi' -> T (cToF centi'))
    <$> (centi_fn (fToC faren))
-- cToF & fToC as Converter-Functions defined someplace else
\end{minted}
\pause
Hiermit kann man dann auch Funktionen, die auf Grad-Celsius rechnen auf
Daten anwenden, die eigenlich nur Fahrenheit speichern, aber eine
Umrechnung bereitstellen.\\Analog kann man auch einen Zeit-Datentypen
definieren, der intern mit Sekunden rechnet (und somit garantiert frei
von Fehlern wie -3 Minuten oder 37 Stunden ist)
\end{frame}

\subsection{Non-Record Strukturen}
\begin{frame}[fragile]
Das ganze kann man auch parametrisieren und auf Non-Record-Strukturen
anwenden. Beispielhaft an einer Map verdeutlicht:

\begin{minted}{haskell}
-- from Data.Lens.At
at :: Ord k => k -> Lens' (Map k v) (Maybe v)

-- oder identisch, wenn man die Lens' aufloest:
at :: Ord k, forall f. Functor f => 
      k -> (Maybe v -> f Maybe v) -> Map k v -> f Map k v

at k mb_fn m
  = wrap <$> (mb_fn mv)
  where
    mv = Map.lookup k m
    
    wrap :: Maybe v -> Map k v
    wrap (Just v') = Map.insert k v' m
    wrap Nothing   = case mv of
                       Nothing -> m
                       Just _  -> Map.delete k m

-- mb_fn :: Maybe v -> f Maybe v
\end{minted}
\end{frame}

\subsection{Weitere Beispiele}
\begin{frame}[fragile]
\begin{itemize}
\item
  Bitfields auf Strukturen die Bits haben (Ints, \ldots{}) in
  Data.Bits.Lens
  \pause
\item
  Web-scraper in Package hexpat-lens

\begin{minted}{haskell}
p ^.. _HTML' . to allNodes
             . traverse . named "a"
             . traverse . ix "href"
             . filtered isLocal
             . to trimSpaces
\end{minted}

  Zieht alle externen Links aus dem gegebenen HTML-Code in p um weitere
  ziele fürs crawlen zu finden.
\end{itemize}
\end{frame}

\section{Erweiterungen}
\subsection{Functor -> Applicative}
\begin{frame}[fragile]
Bisher hatten wir Lenses nur auf Funktoren F. Die nächstmächtigere
Klasse ist Applicative.
\pause
\begin{minted}{haskell}
type Traversal' s a = forall f. Applicative f
                             => (a -> f a) -> (s -> f s)
\end{minted}
\pause
Da wir den Container identisch lassen (weder s noch a wurde angefasst)
muss sich etwas anderes ändern. Statt eines einzelnen Focus erhalten wir
viele Foci.
\end{frame}

\begin{frame}[fragile]
Recap: Was macht eine Lens:

\begin{minted}{haskell}
data Adress = A { _road :: String
                , _city :: String
                , _postcode :: String }

road :: Lens' Adress String
road elt_fn (A r c p) = (\r' -> A r' c p) <$> (elt_fn r)
--                      |    "Hole"     |     | Thing to put in|
\end{minted}
\pause
Wenn man nun road \& city gleichzeitig bearbeiten will:

\begin{minted}{haskell}
addr_strs :: Traversal' Address String
addr_strs elt_fn (A r c p)
  = ... (\r' c' -> A r' c' p)    .. (elt_fn r) .. (elt_fn c) ..
--      | function with 2 "Holes"|  first Thing |  second Thing
\end{minted}
\end{frame}

\begin{frame}[fragile]
fmap kann nur 1 Loch stopfen, aber nicht mit n Löchern umgehen.
Applicative mit \texttt{<*>} kann das.\\Somit gibt sich
\pause
\begin{minted}{haskell}
addr_strs :: Traversal' Address String
addr_strs elt_fn (A r c p)
  = pure           (\r' c' -> A r' c' p)  <*> (elt_fn r) <*> (elt_fn c)
--  lift in Appl. | function with 2 "Holes"|  first Thing |  second Thing
-- oder kürzer
addr_strs :: Traversal' Address String
addr_strs elt_fn (A r c p)
  = (\r' c' -> A r' c' p)  <$> (elt_fn r) <*> (elt_fn c)
-- pure x <*> y == x <$> y
\end{minted}
\end{frame}

\begin{frame}[fragile]
Wie würd eine modify-funktion aussehen?
\pause
\begin{minted}{haskell}
over :: Lens' s a -> (a -> a) -> s -> s
over ln f = runIdentity . ln (Identity . f)
\end{minted}
\pause
\begin{minted}{haskell}
over :: Traversal' s a -> (a -> a) -> s -> s
over ln f = runIdentity . ln (Identity . f)
\end{minted}
\pause
Der Code ist derselbe - nur der Typ ist generischer. Auch die anderen
Dinge funktioniert diese Erweiterung (für Identity und Const muss man
noch ein paar dummy-Instanzen schreiben um sie von Functor auf
Applicative oder Monad zu heben - konkret reicht hier die Instanzierung
von Monoid). In der Lens-Library ist daher meist Monad m statt Functor f
gefordert.
\end{frame}

\subsection{Wozu dienen die Erweiterungen?}
\begin{frame}[fragile]
Man kann mit Foci sehr selektiv vorgehen. Auch kann man diese durch
Funktionen steuern. Beispisweise eine Funktion anwenden auf

\begin{itemize}
\item
  Jedes 2. Listenelement
\item
  Alle graden Elemente in einem Baum
\item
  Alle Namen in einer Tabelle, deren Gehalt \textgreater{} 10.000 EUR ist
\end{itemize}
\pause
Traversals und Lenses kann man trivial kombinieren (lens . lens
$\Rightarrow$ lens, lens . traversal $\Rightarrow$ traversal etc.)
\end{frame}

\subsection{Wie es in Lens wirklich aussieht}
\begin{frame}[fragile]
In dieser Vorlesung wurde nur auf Monomorphic Lenses eingegangen. In der
richtigen Library ist eine Lens

\begin{minted}{haskell}
type Lens' s a = Lens s s a a
type Lens s t a b = forall f. Functor f => (a -> f b) -> (s -> f t)
\end{minted}
\pause
sodass sich auch die Typen ändern können um z.B. automatisch einen
Konvertierten (sicheren) Typen aus einer unsicheren Datenstruktur zu
geben.\\
\pause
Die modify-Funktion over ist auch

\begin{minted}{haskell}
over :: Profunctor p => Setting p s t a b -> p a b -> s -> t
\end{minted}
\end{frame}

\begin{frame}[fragile]
\emph{Edward is deeply in thrall to abstractionitis} - Simon Peyton
Jones
\pause
Lens alleine definiert 39 newtypes, 34 data-types und 194
Typsynonyme\ldots{}\\

\pause
Ausschnitt

\begin{minted}{haskell}
-- traverseOf :: Functor f => Iso s t a b           -> (a -> f b) -> s -> f t
-- traverseOf :: Functor f => Lens s t a b          -> (a -> f b) -> s -> f t
-- traverseOf :: Applicative f => Traversal s t a b -> (a -> f b) -> s -> f t

traverseOf :: Over p f s t a b -> p a (f b) -> s -> f t
\end{minted}

\pause
dafuq?
\end{frame}


\end{document}