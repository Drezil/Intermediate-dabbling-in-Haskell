\documentclass{beamer}

\usepackage[utf8]{inputenc}
\usepackage[T1]{fontenc}
\usepackage[ngerman]{babel}
\usepackage{graphicx} % Bilder
\usepackage{wrapfig} % Umflussbilder
\usepackage{multicol} % Multiple columns
\usepackage{minted} % Haskell source code
\usepackage{framed} % Frames around source code
\usepackage[framemethod=tikz]{mdframed} % Frames
\usepackage{verbatim} % \begin{comment}...\end{comment}
\usepackage{etoolbox} % manipulate minted
\AtBeginEnvironment{minted}{\fontsize{10}{10}\selectfont}
\AfterEndEnvironment{minted}{}

\mdfdefinestyle{fancy}{
  roundcorner=5pt,
  linewidth=4pt,
  linecolor=red!80,
  backgroundcolor=red!20
}
\newmdenv[style=fancy]{important}

% Stuff for Beamer
\beamertemplatenavigationsymbolsempty
\usetheme{Warsaw}

\title{Intermediate Dabbling in Haskell}

\begin{document}

%  \usebackgroundtemplate{\includegraphics[width=\paperwidth,height=\paperheight]{1.jpg}} 
  
%----------------------------------------------------------------------------------------  

  \begin{frame}
  \begin{center}
    \Huge\textbf{Intermediate Functional Programming in Haskell}\\ \bigskip
    \LARGE Universität Bielefeld, Sommersemester 2015\\ \bigskip
    \large Jonas Betzendahl \& Stefan Dresselhaus
    \end{center}
  \end{frame}

%----------------------------------------------------------------------------------------  
\begin{frame}[allowframebreaks]{Outline}
\frametitle{Übersicht}
\tableofcontents[hideallsubsections]
\end{frame}

\section{State-Monad}

\subsection{Beispiele}
\begin{frame}[fragile]
Wir hatten in der letzten Vorlesung die State-Monade kurz angesprochen.\\
Heute wenden wir uns der Definition zu und werden herausfinden, wie man noch weiter abstrahieren kann.\\
\end{frame}

\begin{frame}[fragile]
Beispiel:
\begin{minted}{haskell}
countme :: a -> State Int a
countme a = do
              modify (+1)
              return a

example :: State Int Int
example = do
              x <- countme (2+2)
              y <- return (x*x)
              z <- countme (y-2)
              return z

examplemain = runState example 0
-- -> (14,2), 14 = wert von z, 2 = interner counter
\end{minted}
\end{frame}

\begin{frame}[fragile]
Beispiel 2:
\begin{minted}{haskell}
module Main where
import Control.Monad.State
type CountValue = Int
type CountState = (Bool, Int)
 
startState :: CountState
startState = (False, 0)

play :: String -> State CountState CountValue
--play  ...

\end{minted}
\end{frame}

\begin{frame}[fragile]
\begin{minted}{haskell}
play []     = do
              (_, score) <- get
              return score
play (x:xs) = do
 (on, score) <- get
 case x of
   'C' -> if on then put (on, score + 1) else put (on, score)
   'A' -> if on then put (on, score - 1) else put (on, score)
   'T' -> put (False, score)
   'G' -> put (True, score)
   _   -> put (on, score)
 playGame xs

main = print $ runState (play "GACAACTCGAAT") startState
-- -> (-3,(False,-3))
\end{minted}
\end{frame}

\subsection{Definition}
\begin{frame}[fragile]
Die State-Monade \glqq packt\grqq \ einen State in jeden Funktionsaufruf:
\begin{minted}{haskell}
newtype State s a = State { runState :: s -> (a,s) }

foo :: a -> State s b
foo :: a -> (s -> (b,s))
foo :: a -> s -> (b,s)
\end{minted}
\pause
Wir sehen, dass eine Funktion, die in die State-Monade aufgewertet wurde einfach nur ein weiteres Funktionsargument (den State \texttt{s}) mitgegeben wird und wir statt dem Ergebnis \texttt{b} ein \texttt{(b,s)} bekommen, was den neuen Zustand enthält.
\end{frame}

\subsection{Functor}
\begin{frame}[fragile]
Man kann sich die State-Monade als Berechnung vorstellen, die noch nicht ausgeführt werden kann, weil der initiale State nicht gesetzt ist. Dies spiegelt sich auch in der Funktor-Instanz wieder:
\pause
\begin{minted}{haskell}
instance Functor (State s) where
  fmap f (State rs) = State $ \s -> 
                      let (a,s') = rs s in (f a, s')
\end{minted}
\pause
Wir sehen, dass wir erst mit \texttt{rs s} den State, den wir bekommen \glqq ausführen\grqq \ müssen um ein \texttt{a} zu generieren, auf das wir die Funktion anwenden können.\\
Anschließend verpacken wir in unserem Ergebnis den modifizierten State und die angewendete Funktion.\\
\pause
Wichtig ist hier, dass wir wieder eine Funktion in \texttt{State} verpackt zurückgeben müssen, die einen State nimmt:
\begin{minted}{haskell}
newtype State s a = State { runState :: s -> (a,s) }
\end{minted}

\end{frame}
\subsection{Applicative}

\begin{frame}[fragile]
Ganz analog funktioniert die Applicative-Instanz:
\begin{minted}{haskell}
instance Applicative (State s) where
    pure a = State $ \s -> (a,s)
    (State rs) <*> (State rest) =
                State $ \s ->
                      let (f,s') = rs s
                          (a,s'') = rest s'
                      in
                          (f a,s'')
\end{minted}
\pause
Hier müssen wir den State 2x ausführen. Einmal um an das f zu kommen und dann verketten wir dies mit der restlichen State-Berechnung um auch noch an unser a zu kommen. Zurück geben wir den doppelt bearbeiteten State und den bearbeiteten Wert.\\
\pause
Wichtig ist hier die Reihenfolge! Wir hätten es auch umdrehen können:
\begin{minted}{haskell}
let (f,s'') = rs s'
    (a,s') = rest s
in
    (f a,s'')
\end{minted}
allerdings arbeitet \texttt{<*>} immer von links nach rechts!
\end{frame}
\subsection{Monad}

\begin{frame}[fragile]
Am wichtigsten ist die Monad-Instanz:
\begin{minted}{haskell}
instance Monad (State s) where
    return a = State $ \s -> (a,s)
    (State rs) >>= f =
                State $ \s ->
                      let (a,s') = rs s
                          (State rs') = f a
                      in
                          rs' s'
\end{minted}
\pause
Wir müssen wieder zuerst den State ausführen um an unser \texttt{a} zu gelangen. Danach können wir unsere Funktion \texttt{f} ausführen um eine neue Funktion zu bekommen, die wir auch aus dem State auspacken. Eine kleine Anwendung des erhaltenen States hierauf gibt uns schlussendlich unser Ergebnis.
\end{frame}

\section{Monad-Transformer}

\subsection{Beispiel}
\begin{frame}[fragile]
Wir hatten letzte Woche die Maybe-Monade mit dem folgenden Anwendugsfall:
\begin{minted}{haskell}
f = do
      folder <- getInbox
      mail   <- getFirstMail folder
      header <- getHeader mail
      return header
\end{minted}
Nun möchten wir aus irgendeinem Grund (Logging, Netzwerk, ..) zwischen dem \texttt{getInbox} und dem \texttt{getFirstMail} eine IO-Aktion ausführen.\\
~\\
Problem: \texttt{IO /= Maybe}\\
\pause
Als Konsequenz können wir die do-notation nicht verwenden - wir fallen also wieder zurück auf die hässliche Notation:
\end{frame}

\begin{frame}[fragile]
\begin{minted}{haskell}
f :: IO (Maybe Header)
f = case getInbox of
      (Just folder) -> 
          do
            putStrLn "debug"
            return $ case getFirstMail folder of
              (Just mail) -> 
                  case getHeader mail of
                    (Just head) -> return head
                    Nothing     -> Nothing
              Nothing     -> Nothing
      Nothing       -> return Nothing
\end{minted}
\end{frame}

\begin{frame}[fragile]
Dieser Code ist ohne Frage hässlich. Stellt sich die Frage, ob wir nicht soetwas, wie \texttt{MaybeIO} bauen können, sodass wir wieder do-notation verwenden können.\\
\pause
Also kombinieren wir es (ähnlich zur State-Monade):
\begin{minted}{haskell}
data MaybeIO a = MaybeIO { runMaybeIO :: IO (Maybe a) }
\end{minted}
\pause
Dieses liefert uns 2 Funktionen:
\begin{minted}{haskell}
MaybeIO    :: IO (Maybe a) -> MaybeIO a
runMaybeIO :: MaybeIO a -> IO (Maybe a)
\end{minted}
Also eine Funktion, um in unsere neue Monade zu kommen und eine Funktion um dieses wieder Rückgängig zu machen.
\end{frame}

\subsection{F/A/M}
\begin{frame}[fragile]
Fangen wir mit der Functor-Instanz an:
\begin{minted}{haskell}
instance Functor MaybeIO where
  fmap f input = wrapped
               where
                 unwrapped = runMaybeIO input
                 -- IO (Maybe a) auspacken
                 fmapped = fmap (fmap f) unwrapped
                 -- erstes fmap mapped durch IO,
                 -- zweites fmap durch Maybe
                 wrapped = MaybeIO fmapped
                 -- einpacken in den richtigen Typen
\end{minted}
\pause
oder kurz:
\begin{minted}{haskell}
instance Functor MaybeIO where
  fmap f = MaybeIO . fmap (fmap f) . runMaybeIO
\end{minted}
\end{frame}

\begin{frame}[fragile]
Applicative:
\begin{minted}{haskell}
instance Applicative MaybeIO where
  pure    = MaybeIO . pure . Just
            -- in Just packen, mit pure in IO heben
            -- und den Typen mit MaybeIO aliasen
  f <*> x = MaybeIO $ (<*>) <$> f' <*> x'
            where
              f' = runMaybeIO f -- IO (Maybe f)
              x' = runMaybeIO x -- IO (Maybe x)
\end{minted}
Das erste \texttt{(<*>)} ist Applicative auf Maybe und es wird in Applicative \texttt{<*>} von IO hineingemappt.
\end{frame}

\begin{frame}[fragile]
Monad:
\begin{minted}{haskell}
instance Monad MaybeIO where
  return = pure
  x >>= f = MaybeIO $ x''
            where
              x' = runMaybeIO x
              x'' = x' >>= runMaybeIO . mb . fmap f
              mb :: Maybe (MaybeIO a) -> MaybeIO a
              mb (Just a) = a
              mb Nothing = MaybeIO $ return Nothing
\end{minted}
Zuerst packen wir das MaybeIO aus. \texttt{fmap f} bringt uns ein Maybe (MaybeIO a), welches wir mittels der Hilfsfunktion \texttt{mb} auspacken oder einen leeren Wert konstruieren.\\
Dieses jagen wir noch durch runMaybeIO um wieder ein \texttt{IO (Maybe a)} zu bekommen, auf das wir dann den \texttt{>==}-Operator von IO anwenden können. Das Ergebnis verpacken wir noch in \texttt{MaybeIO} und sind fertig.
\end{frame}

\subsection{Beispiel revisited}
\begin{frame}[fragile]
Da wir nun eine Monade definiert haben, können wir ja wieder do nutzen:
\begin{minted}{haskell}
f = do
     i <- getInbox
     putStrLn "debug"
     m <- getFirstMail i
     h <- getHeader m
     return h
\end{minted}
\end{frame}
\begin{frame}[fragile]
Allerdings:
\begin{minted}{text}
    Couldn't match type Maybe with MaybeIO
    Expected type: MaybeIO Inbox
      Actual type: Maybe Inbox
    In a stmt of a 'do' block: in <- getInbox

    Couldn't match type IO with MaybeIO
    Expected type: MaybeIO ()
      Actual type: IO ()
    In a stmt of a 'do' block: putStrLn "debug"

    Couldn't match type Maybe with MaybeIO
    Expected type: MaybeIO Mail
      Actual type: Maybe Mail
    In a stmt of a 'do' block: m <- getFirstMail i
    
    Couldn't match type Maybe with MaybeIO
    Expected type: MaybeIO Header
      Actual type: Maybe Header
    In a stmt of a 'do' block: h <- getHeader m
\end{minted}
\end{frame}

\begin{frame}[fragile]
Wir brauchen also Konverter:
\begin{itemize}
 \item \texttt{Maybe -> MaybeIO}
 \item \texttt{IO -> MaybeIO}
\end{itemize}
\pause
Aber wir haben schon alles, was wir brauchen, wenn wir uns nur klar machen:
\begin{minted}{haskell}
return  :: Maybe a -> IO (Maybe a) -- return von IO
MaybeIO :: IO (Maybe a) -> MaybeIO a
\end{minted}
\pause
und
\begin{minted}{haskell}
Just      :: a -> Maybe a
fmap Just :: IO a -> IO (Maybe a)
\end{minted}
\end{frame}

\begin{frame}[fragile]
Somit wird unser Code von oben:
\begin{minted}{haskell}
f = do
     i <- MaybeIO (return (getInbox))
     MaybeIO (fmap Just (putStrLn "debug"))
     m <- MaybeIO (return (getFirstMail i))
     h <- MaybeIO (return (getHeader m))
     return h
\end{minted}
\pause
Zwar können wir nun do nutzen, aber das sieht doch eher hässlich aus. Außerdem ist so viel Code doppelt!
\end{frame}

\subsection{Finale Version}

\begin{frame}[fragile]
Wenn wir Muster finden, dann faktorisieren wir sie doch raus!
\begin{minted}{haskell}
liftMaybe :: Maybe a -> MaybeIO a
liftMaybe x = MaybeIO (return x)

liftIO :: IO a -> MaybeIO a
liftIO x = MaybeIO (fmap Just x)
\end{minted}
\pause
und wir erhalten:
\begin{minted}{haskell}
f = do
     i <- liftMaybe getInbox
     liftIO $ putStrLn "debug"
     m <- liftMaybe $ getFirstMail i
     h <- liftMaybe $ getHeader m
     return h
\end{minted}
\end{frame}

\section{Monad-Transformer cont.}

\subsection{Recap}

\begin{frame}[fragile]
Wenn wir uns nochmals ansehen, welche Eigenschaft der IO-Monade wir genutzt haben, dann fällt uns auf:
\pause
\begin{minted}{haskell}
instance Functor MaybeIO where
  fmap f = MaybeIO . fmap (fmap f) . runMaybeIO
\end{minted}
\texttt{fmap} von IO als Funktor
\pause
\begin{minted}{haskell}
instance Applicative MaybeIO where
  pure    = MaybeIO . pure . Just
  f <*> x = MaybeIO $ (<*>) <$> (runMaybeIO f)
                            <*> (runMaybeIO x)
\end{minted}
\texttt{pure} und \texttt{<*>} von IO als Applicative
\pause
\begin{minted}{haskell}
instance Monad MaybeIO where
  return = pure
  x >>= f = MaybeIO $ (runMaybeIO x)
                      >>= runMaybeIO . mb . fmap f
            where
              mb (Just a) = a
              mb Nothing = MaybeIO $ return Nothing
\end{minted}
\texttt{return} von IO
\end{frame}

\begin{frame}[fragile]
Uns fällt auf: Wir verwenden gar keine intrisischen Eigenschaften von IO.\\
Also können wir IO auch durch jede Monade ersetzten. Dies nennt man dann Monad Transformer.
\begin{minted}{haskell}
data MaybeT m a = MaybeT { runMaybeT :: m (Maybe a) }
\end{minted}
\pause
Und der Code von eben wird zu:
\begin{minted}{haskell}
instance Functor m => Functor (MaybeT m) where
  fmap f = MaybeT . fmap (fmap f) . runMaybeT

instance Applicative m => Applicative (MaybeT m) where
  pure    = MaybeT . pure . Just
  f <*> x = MaybeT $ (<*>) <$> (runMaybeT f)
                           <*> (runMaybeT x)

instance Monad m => Monad (MaybeT m) where
  return = pure
  x >>= f = MaybeT $ (runMaybeT x)
                     >>= runMaybeT . mb . fmap f
            where
              mb (Just a) = a
              mb Nothing = MaybeT $ return Nothing
\end{minted}
\pause
Wir haben nur die Monade heraus faktorisiert.
\end{frame}

\begin{frame}[fragile]
Frage: Wie realisieren wir nun \texttt{liftIO} etc.?\\
\pause
Über Typklassen!
\begin{minted}{haskell}
class Monad m => MonadIO m where
    liftIO :: IO a -> m a
\end{minted}
Wir verlangen einfach, dass IO irgendwie verarbeitet werden muss.
\pause
\begin{important}
IO ist ein Spezialfall, da IO als Monade nicht additiv ist. Es gibt somit keinen IOT.
\end{important}
\end{frame}

\begin{frame}[fragile]
Frage: Wie realisieren wir nun \texttt{liftIO} etc.?\\
Über Typklassen!
\begin{minted}{haskell}
class MonadTrans t where
    lift :: (Monad m) => m a -> t m a
\end{minted}
\pause
Dies ist die allgemeine Form für additive Monaden. Mit \texttt{lift} heben wir uns eine monadische Ebene höher.\\
Doch was bedeutet das?
\end{frame}

\subsection{Beispiele}

\begin{frame}[fragile]
Wir haben schon ein paar Monaden kennengelernt. Diese sind fast alle additiv. Wir können somit folgendes bauen:
\pause
\begin{minted}{haskell}
data MyMonadStack a = StateT (EitherT (MaybeT (IO a)))
\end{minted}
\pause
Wie schreiben wir nun hierin Code?
\begin{minted}{haskell}
bsp :: MyMonadStack ()
bsp = do
    a <- fun                -- fun  :: MyMonadStack Int
    b <- lift $ fun2        -- fun2 :: EitherT (MaybeT (IO Int))
    c <- lift . lift $ fun3 -- fun3 :: MaybeT (IO Int))
    liftIO $ putStrLn "foo"
\end{minted}

\end{frame}

\begin{frame}[fragile]
Um auf spezielle Ebenen im Monad-Stack zuzugreifen gibt es für jeden Zweck eine Typklasse. Beispielsweise:
\pause
\begin{minted}{haskell}
instance (Monad m) => MonadState s (StateT s m) where
  get   = StateT $ \s -> return (s,s)
  put s = StateT $ \_ -> return ((),s)
\end{minted}
\pause
Nun können wir einfach
\begin{minted}{haskell}
bsp :: MyMonadStack ()
bsp = do
    state <- get
    put $ manipulateState state
    liftIO $ putStrLn "foo"
\end{minted}
\pause
Analog gibt es dies auch für \texttt{ReaderT} (\texttt{env}), welches ein read-only-Environment bereitstellt (z.b. eine Konfiguration) oder für \texttt{WriterT} (\texttt{tell}), welches ein write-only-Environment zur Verfügung stellt (z.b. Logging). Häufig findet man daher einen Read-Write-State-Transformer, kurz \texttt{RWST}-Stack.\\
\pause
Echtweltprogramme sind meist durch eine \texttt{RWST IO} mit der Außenwelt verbunden.
\end{frame}

\begin{frame}[fragile]
Ein weiteres Echtwelt-Beispiel könnte etwa der folgende Aufruf sein:
\begin{minted}{haskell}
data Env = Env { filename :: String }

readInputs :: ReaderT Env IO String
readInputs = do
           e <- env
           f <- liftIO $ readFile (filename e)
           return f
\end{minted}
\pause
Dieser Aufruf liest einen Dateinamen aus einem Environment, kann per \texttt{liftIO} IO-Aktionen ausführen und das Ergebnis (den String mit dem Dateiinhalt) zurückliefern.
\end{frame}

\begin{frame}[fragile]
Noch ein Beispiel aus einem Spiel könnte sein:
\begin{minted}{haskell}
mainLoop :: RWST Env () State IO ()
mainLoop = do
         e <- env
         f <- liftIO $ getUserInput (keySettings e)
         newWorld <- updateWorld f -- holt sich die Welt mittels 'get'
         put newWorld
         unless (f == endKey e) mainLoop
\end{minted}
\pause
Dies ist ein klassisches Game-Loop, bestehend aus Konfigurationen im Env (Key settings), IO (User-Input abfragen), Update des internen Zustands (updateWorld) und das schreiben des neuen Zustandes (put newWorld). Sofern dann das Spiel nicht beendet ist loopen wir.
\end{frame}



\end{document}